// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

contract PoWRewards is Ownable, ReentrancyGuard {
    using SafeERC20 for IERC20;

    uint256 public lastUpdatedBlock;

    mapping(address => uint256) public glideRewards;

    IERC20 public immutable token; // Glide token

    event AddReward(address indexed user, uint256 block, uint256 amount);
    event WithdrawReward(address indexed user, uint256 block, uint256 amount);
    event RescueTokens(
        address indexed user,
        address indexed token,
        uint256 amount
    );

    constructor(
        IERC20 _token,
        uint256 _startBlock,
        address initialOwner
    ) Ownable(initialOwner) {
        token = _token;
        lastUpdatedBlock = _startBlock;
    }

    receive() external payable {}

    function setLastUpdateBlock(uint256 _lastUpdatedBlock) external onlyOwner {
        lastUpdatedBlock = _lastUpdatedBlock;
    }

    function addGlideReward(address _user, uint256 _amount) external onlyOwner {
        glideRewards[_user] += _amount;

        token.safeTransferFrom(msg.sender, address(this), _amount);

        emit AddReward(_user, block.number, _amount);
    }

    function addGlideReward(
        address[] memory _users,
        uint256[] memory _amounts
    ) external onlyOwner {
        require(_users.length == _amounts.length, "Arrays are not equal");

        for (uint256 i = 0; i < _users.length; i++) {
            glideRewards[_users[i]] += _amounts[i];

            token.safeTransferFrom(msg.sender, address(this), _amounts[i]);

            emit AddReward(_users[i], block.number, _amounts[i]);
        }
    }

    function withdrawGlideReward() external nonReentrant {
        if (glideRewards[msg.sender] > 0) {
            uint256 amount = glideRewards[msg.sender];

            glideRewards[msg.sender] = 0;

            token.safeTransfer(msg.sender, amount);

            emit WithdrawReward(msg.sender, block.number, amount);
        }
    }

    function rescueToken(
        address rewardToken,
        address addressForReceive
    ) external onlyOwner {
        uint256 balance = IERC20(rewardToken).balanceOf(address(this));
        IERC20(token).safeTransfer(addressForReceive, balance);
        emit RescueTokens(msg.sender, rewardToken, balance);
    }
}
